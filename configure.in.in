AM_INIT_AUTOMAKE(krecipes, 0.7)
AC_C_BIGENDIAN
AC_CHECK_KDEMAXPATHLEN

AC_ARG_ENABLE(sqlite,    [  --disable-sqlite]     [Don't compile SQLite backend support],disable_sqlite=yes)
AC_ARG_ENABLE(mysql,     [  --disable-mysql]      [Don't compile MySQL backend support],disable_mysql=yes)
AC_ARG_ENABLE(postgresql,[  --disable-postgresql] [Don't compile PostgreSQL backend support],disable_postgresql=yes)

dnl ----- Check if we should enable MySQL ------

if test "x$disable_mysql" != "xyes"; then
  AC_DEFINE(HAVE_MYSQL, 1, [have MySQL])
  have_mysql="true"
else
  AC_DEFINE(HAVE_MYSQL, 0, [have MySQL])
  have_mysql="false"
fi


dnl ----- Check if we should enable PostgreSQL ------

if test "x$disable_postgresql" != "xyes"; then
  AC_DEFINE(HAVE_POSTGRESQL, 1, [have PostgreSQL])
  have_postgresql="true"
else
  AC_DEFINE(HAVE_POSTGRESQL, 0, [have PostgreSQL])
  have_postgresql="false"
fi


dnl ------ Check for the SQLite headers -----

AC_DEFUN([AC_HAVE_SQLITE],
[
        AC_DEFINE(HAVE_SQLITE, 1, [have SQLite])
        have_sqlite=true
	SQLITE_LIB="-lsqlite"
	AC_SUBST(SQLITE_LIB)
])

AC_DEFUN([AC_NO_SQLITE],
[
        AC_DEFINE(HAVE_SQLITE, 0, [have SQLite])
        have_sqlite=false
])

AC_DEFUN([AC_HAVE_SQLITE3],
[
        AC_DEFINE(HAVE_SQLITE3, 1, [have SQLite3])
        have_sqlite3=true
	AC_NO_SQLITE
	SQLITE_LIB="-lsqlite3"
	AC_SUBST(SQLITE_LIB)
])

AC_DEFUN([AC_NO_SQLITE3],
[
        AC_DEFINE(HAVE_SQLITE3, 0, [have SQLite3])
        have_sqlite3=false
])

if test "x$disable_sqlite" != "xyes"; then
  KDE_CHECK_HEADER(sqlite.h,
           AC_HAVE_SQLITE,
           AC_NO_SQLITE
  )

  KDE_CHECK_HEADER(sqlite3.h,
           AC_HAVE_SQLITE3,
           AC_NO_SQLITE3
  )
else
  AC_NO_SQLITE
  AC_NO_SQLITE3
fi

dnl ----------- Should I link or should I not link? ---------
AM_CONDITIONAL(link_lib_MYSQL, test x$have_mysql = xtrue)
AM_CONDITIONAL(link_lib_POSTGRESQL, test x$have_postgresql = xtrue)
AM_CONDITIONAL(link_lib_SQLITE, test x$have_sqlite = xtrue || test x$have_sqlite3 = xtrue)

dnl ----------- Should Krecipes be built at all? ------------
if test "x$have_mysql" = "xfalse" && test "x$have_postgresql" = "xfalse"; then
        if test "x$have_sqlite" = "xfalse" && test "x$have_sqlite3" = "xfalse"; then
        DO_NOT_COMPILE="$DO_NOT_COMPILE krecipes"
        will_not_build_krecipes=true
        fi
fi
